#!/usr/bin/perl

# $Id: adj-mi2cc 597 2010-06-23 01:06:56Z zola $
#
# File: adj-mi2cc
# Developed: Jul 12, 2009
#
# Author: Jaroslaw Zola <jaroslaw.zola@gmail.com>
#
# Copyright 2007-2010 Jaroslaw Zola
#
# This file is part of TINGe.
#
# TINGe is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# TINGe is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with TINGe. If not, see <http://www.gnu.org/licenses/>.

use strict;
use warnings;


sub read_adj {
    my $infile = $_[0];
    my @edges;
    open(INPUT, "< $infile") or die "error: can't open input file\n";

    # skip comments
    my $input;
    while ((defined ($input = <INPUT>)) && ($input =~ m/^>/)) { }

    if (defined $input) {
	# process data
	do {
	    chomp($input);

	    my @line = split("\t", $input);
	    my $len = scalar(@line);
	    die "error: data corrupted\n" if (($len < 3) || (($len - 1) % 2 != 0));

	    my $s = $line[0];
	    for (my $i = 1; $i < $len; $i += 2) {
		my $t = $line[$i];
		my $edge = "$s\t$t\t$line[$i + 1]";
		$edge = "$t\t$s\t$line[$i + 1]" if ($t lt $s);
		push @edges, $edge;
	    }
	} while ($input = <INPUT>);
    }

    close(INPUT);

    # make array unique
    my %keys;
    @keys{@edges} = ();
    @edges = keys %keys;

    return @edges;
}

my $usage = "usage: adj-mi2cc infile [rev]\n";
die $usage if ((@ARGV != 1) && (@ARGV != 2));

my $infile = $ARGV[0];
my $rev = "CC";
$rev = "1 - CC" if (@ARGV == 2);

# read adj
my @edges = read_adj($infile);

# transform
for my $i (0 .. scalar(@edges) - 1) {
    my ($s, $t, $w) = split("\t", $edges[$i]);
    $w = sqrt(1 - exp(-2 * $w));
    $w = 1 - $w if ($rev eq "1 - CC");
    $edges[$i] = "$s\t$t\t$w";
}

@edges = sort(@edges);

# rewrite adj header
open(INPUT, "< $infile") or die "error: can't open input file\n";

my $input;
while ((defined ($input = <INPUT>)) && ($input =~ m/^>/)) { print $input; }

close(INPUT);

print "> Converted to ................. $rev";

# generate output
my $ls = "";
my $lt = "";

foreach (@edges) {
    my ($s, $t, $w) = split("\t", $_);
    next if ($s eq $t);
    if (($s ne $ls) || ($t ne $lt)) {
	print "\n$s" if ($s ne $ls);
	print "\t$t\t";
	printf '%.5f', $w;
    }
    $ls = $s;
    $lt = $t;
}

print "\n";
